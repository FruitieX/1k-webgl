<script id="v" type="x-shader/x-vertex">
attribute vec2 p;
  void main() {
    gl_Position = vec4(p, 0, 1);
  }
</script>

<script id="f" type="x-shader/x-fragment">
precision mediump float;
//precision highp float;
uniform float t;
uniform vec2 r;

/*
float boundSin(float x) {
  return sin(x) / 2.0 + 0.5;
}
*/

// antialias
// TODO: in production get rid of #if statements
#define AA 1   // make this 1 is your machine is too slow

//------------------------------------------------------------------

float sdPlane( vec3 p )
{
	return p.y;
}

float sdSphere( vec3 p, float s )
{
    return length(p)-s;
}

//------------------------------------------------------------------

/*
float opS( float d1, float d2 )
{
    return max(-d2,d1);
}
*/

vec2 opU( vec2 d1, vec2 d2 )
{
	return (d1.x<d2.x) ? d1 : d2;
}

/*
vec3 opRep( vec3 p, vec3 c )
{
    return mod(p,c)-0.5*c;
}
*/

/*
vec3 opTwist( vec3 p )
{
    float  c = cos(10.0*p.y+10.0);
    float  s = sin(10.0*p.y+10.0);
    mat2   m = mat2(c,-s,s,c);
    return vec3(m*p.xz,p.y);
}
*/

float PI = 3.14;

//------------------------------------------------------------------
float calcPlasma(float x, float y, float z, float t) {
  // horizontal sinusoid
  float sine1 = sin(x * 10.0 + t * 2.0);

  // rotating sinusoid
  float sine2 = sin(10.0 * (x * sin(t / 2.0) + z * cos(t / 3.0)) + t);

  // circular sinusoid
  float cx = x + 0.5 * sin(t / 5.0);
  float cy = y + 0.5 * cos(t / 3.0);
  float sine3 = sin(sqrt(100.0 * (cx * cx + cy * cy) + 1.0) + t);

  float blend = sine1 + sine2 + sine3;

  blend *= 1.0 + sin(t / 4.0) * 2.0;
  //blend *= 3.0;
  blend = sin(blend * PI / 2.0) / 2.0 + 0.5;
  //blend = pow(blend, 2.0);
  return blend;
}

vec2 map( in vec3 pos )
{
    float plasma = calcPlasma(pos.x, pos.y, pos.z, t / 10.0);
    //float hue = t * 10.0;
    float hue = sin(plasma) * 100.0 + t * 10.0;

    vec2 res = opU(vec2(sdPlane(pos), 0.0),

      // Sphere
      vec2(sdSphere(pos-vec3(1.0,0.5,0.0), 0.5 )
        + plasma * sin(t / 20.0)
        //+ sin(0.2 * sin(t / 8.0)) // size
        /*
        + 1.0 / 100.0
        * sin(
          5.0*sin(10.0*pos.x)
          + 2.0*sin(sin(t)*12.0*pos.y)
          + 4.0*sin(sin(t)*13.0*pos.z)
        )
        */
        , hue
      )
    );

    return res;
}

vec2 castRay( in vec3 ro, in vec3 rd )
{
    float tmin = 1.0;
    float tmax = 20.0;

#if 1
    // bounding volume
    float tp1 = (0.0-ro.y)/rd.y; if( tp1>0.0 ) tmax = min( tmax, tp1 );
    float tp2 = (1.6-ro.y)/rd.y; if( tp2>0.0 ) { if( ro.y>1.6 ) tmin = max( tmin, tp2 );
                                                 else           tmax = min( tmax, tp2 ); }
#endif

    float t = tmin;
    float m = -1.0;
    for( int i=0; i<64; i++ )
    {
	    float precis = 0.000001*t;
	    vec2 res = map( ro+rd*t );
        if( res.x<precis || t>tmax ) break;
        t += res.x;
	    m = res.y;
    }

    if( t>tmax ) m=-1.0;
    return vec2( t, m );
}


float softshadow( in vec3 ro, in vec3 rd, in float mint, in float tmax )
{
	float res = 1.0;
    float t = mint;
    for( int i=0; i<16; i++ )
    {
		float h = map( ro + rd*t ).x;
        res = min( res, 8.0*h/t );
        t += clamp( h, 0.02, 0.10 );
        if( h<0.001 || t>tmax ) break;
    }
    return clamp( res, 0.0, 1.0 );
}

vec3 calcNormal( in vec3 pos )
{
    vec2 e = vec2(1.0,-1.0)*0.5773*0.0005;
    return normalize( e.xyy*map( pos + e.xyy ).x +
					  e.yyx*map( pos + e.yyx ).x +
					  e.yxy*map( pos + e.yxy ).x +
					  e.xxx*map( pos + e.xxx ).x );
    /*
	vec3 eps = vec3( 0.0005, 0.0, 0.0 );
	vec3 nor = vec3(
	    map(pos+eps.xyy).x - map(pos-eps.xyy).x,
	    map(pos+eps.yxy).x - map(pos-eps.yxy).x,
	    map(pos+eps.yyx).x - map(pos-eps.yyx).x );
	return normalize(nor);
	*/
}

float calcAO( in vec3 pos, in vec3 nor )
{
	float occ = 0.0;
    float sca = 1.0;
    for( int i=0; i<5; i++ )
    {
        float hr = 0.01 + 0.12*float(i)/4.0;
        vec3 aopos =  nor * hr + pos;
        float dd = map( aopos ).x;
        occ += -(dd-hr)*sca;
        sca *= 0.95;
    }
    return clamp( 1.0 - 3.0*occ, 0.0, 1.0 );
}

vec3 render( in vec3 ro, in vec3 rd )
{
    vec3 col = vec3(0.7, 0.9, 1.0) +rd.y*0.8;
    vec2 res = castRay(ro,rd);
    float t = res.x;
	float m = res.y;
    if( m>-0.5 )
    {
        vec3 pos = ro + t*rd;
        vec3 nor = calcNormal( pos );
        vec3 ref = reflect( rd, nor );

        // material
		col = 0.45 + 0.35*sin( vec3(0.05,0.08,0.10)*(m-1.0) );
    /*
        if( m<1.5 )
        {

            float f = mod( floor(5.0*pos.z) + floor(5.0*pos.x), 2.0);
            col = 0.3 + 0.1*f*vec3(1.0);
        }
        */

        // lighitng
        float occ = calcAO( pos, nor );
		vec3  lig = normalize( vec3(-0.4, 0.7, -0.6) );
		float amb = clamp( 0.5+0.5*nor.y, 0.0, 1.0 );
        float dif = clamp( dot( nor, lig ), 0.0, 1.0 );
        float bac = clamp( dot( nor, normalize(vec3(-lig.x,0.0,-lig.z))), 0.0, 1.0 )*clamp( 1.0-pos.y,0.0,1.0);
        float dom = smoothstep( -0.1, 0.1, ref.y );
        float fre = pow( clamp(1.0+dot(nor,rd),0.0,1.0), 2.0 );
		float spe = pow(clamp( dot( ref, lig ), 0.0, 1.0 ),16.0);

        dif *= softshadow( pos, lig, 0.02, 2.5 );
        dom *= softshadow( pos, ref, 0.02, 2.5 );

		vec3 lin = vec3(0.0);
        lin += 1.3*dif*vec3(1.0,0.8,0.55);
		lin += 2.0*spe*vec3(1.0,0.9,0.7)*dif;
        lin += 0.4*amb*vec3(0.4,0.6,1.0)*occ;
        lin += 0.5*dom*vec3(0.4,0.6,1.0)*occ;
        lin += 0.5*bac*vec3(0.25,0.25,0.25)*occ;
        lin += 0.25*fre*vec3(1.0,1.0,1.0)*occ;
		col = col*lin;

    	col = mix( col, vec3(0.8,0.9,1.0), 1.0-exp( -0.0002*t*t*t ) );
    }

	return vec3( clamp(col,0.0,1.0) );
}

mat3 setCamera( in vec3 ro, in vec3 ta, float cr )
{
	vec3 cw = normalize(ta-ro);
	vec3 cp = vec3(sin(cr), cos(cr),0.0);
	vec3 cu = normalize( cross(cw,cp) );
	vec3 cv = normalize( cross(cu,cw) );
    return mat3( cu, cv, cw );
}

void main()
{
    vec3 tot = vec3(0.0);
#if AA>1
    for( int m=0; m<AA; m++ )
    for( int n=0; n<AA; n++ )
    {
        // pixel coordinates
        vec2 o = vec2(float(m),float(n)) / float(AA) - 0.5;
        vec2 p = (-r.xy + 2.0*(gl_FragCoord.xy+o))/r.y;
#else
        vec2 p = (-r.xy + 2.0*gl_FragCoord.xy)/r.y;
#endif

		// camera
        //vec3 ro = vec3( -0.5+3.5*cos(0.1*t), 1.0, 0.5 + 4.0*sin(0.1*t) );
        vec3 ro = vec3( -0.5+3.5*cos(0.01*t), 1.0, 0.5 + 4.0*sin(0.01*t) );
        vec3 ta = vec3( -0.5, -0.4, 0.5 );
        // camera-to-world transformation
        mat3 ca = setCamera( ro, ta, 0.0 );
        // ray direction
        vec3 rd = ca * normalize( vec3(p.xy,2.0) );

        // render
        vec3 col = render( ro, rd );

		// gamma
        col = pow( col, vec3(0.4545) );

        tot += col;
#if AA>1
    }
    tot /= float(AA*AA);
#endif


    gl_FragColor = vec4( tot, 1.0 );
}
</script>

<script type="text/javascript">
d = document;

window.onload = _ => {
  canvas        = d.getElementById('c');
  gl            = canvas.getContext('webgl');

  // / 2 because potato PC
  canvas.width  = 1920 / 8;
  canvas.height = 1080 / 8;

  // your problem if aspect ratio is incorrect
  canvas.style='height: 100%; width: 100%';

  // get rid of stupid body margin, FIXME: fewer bytes
  body = d.getElementsByTagName('body')[0];
  body.style='margin: 0';

  gl.viewport(0, 0, gl.drawingBufferWidth, gl.drawingBufferHeight);

  buffer = gl.createBuffer();

  // gl.ARRAY_BUFFER = 34962
  gl.bindBuffer(34962, buffer);
  gl.bufferData(
    gl.ARRAY_BUFFER,
    // TODO: optimize
    new Float32Array([
      -1.0, -1.0,
      1.0, -1.0,
      -1.0,  1.0,
      -1.0,  1.0,
      1.0, -1.0,
    1.0,  1.0]),
    gl.STATIC_DRAW
  );

  // vertex shader
  v = 35633; // gl.VERTEX_SHADER = 35633
  shaderScript = d.getElementById('v');
  shaderSource = shaderScript.text;
  vertexShader = gl.createShader(v);
  gl.shaderSource(vertexShader, shaderSource);
  gl.compileShader(vertexShader);

  // fragment shader
  shaderScript   = d.getElementById('f');
  shaderSource   = shaderScript.text;
  fragmentShader = gl.createShader(v-1); // gl.FRAGMENT_SHADER = 35632
  gl.shaderSource(fragmentShader, shaderSource);
  gl.compileShader(fragmentShader);

  // Check for any compilation error
  // TODO: remove in production
  if (!gl.getShaderParameter(fragmentShader, 35713)) { // gl.COMPILE_STATUS = 35713
      alert(gl.getShaderInfoLog(fragmentShader));
      return;
  }

  program = gl.createProgram();

  gl.attachShader(program, vertexShader);
  gl.attachShader(program, fragmentShader);
  gl.linkProgram(program);
  gl.useProgram(program);

  render();
}

render = t => {
  window.requestAnimationFrame(render, canvas);

  // set the "r" resolution variable
  resolutionUniformLocation = gl.getUniformLocation(program, 'r');
  gl.uniform2f(resolutionUniformLocation, gl.canvas.width, gl.canvas.height);

  // set the "time" variable
  timeUniformlocation = gl.getUniformLocation(program, 't');
  gl.uniform1f(timeUniformlocation, t / 1000);

  positionLocation = gl.getAttribLocation(program, 'p');
  gl.enableVertexAttribArray(positionLocation);
  gl.vertexAttribPointer(positionLocation, 2, 5126, 0, 0, 0); // gl.FLOAT = 5126
  gl.drawArrays(4, 0, 6); // gl.TRIANGLES = 4
}

</script>
<canvas id="c"/>
